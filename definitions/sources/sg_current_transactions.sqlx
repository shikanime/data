config {
  type: "view",
  database: "shikanime-studio-labs",
  schema: "seeker",
  name: "sg_current_transactions",
  columns: {
    time: "The date when the transaction occurred",
    account: "The account where the transaction occurred",
    label: "The transaction label or category",
    description: "Detailed description of the transaction",
    change: "The transaction amount",
    symbol: "The currency or asset symbol of the transaction",
    source: "The source of the transaction"
  },
  assertions: {
    nonNull: [
      "time",
      "account",
      "label",
      "description",
      "change",
      "symbol",
      "source"
    ]
  }
}

SELECT
  TIMESTAMP(PARSE_DATE('%d/%m/%Y', date_de_l_operation), 'Europe/Paris') AS time,
  "current" AS account,
  libelle AS label,
  detail_de_l_ecriture AS description,
  montant_de_l_operation AS change,
  devise AS symbol,
  "sg" AS source
FROM
  ${ref("sg_current_transactions_source")}
